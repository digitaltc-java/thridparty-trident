plugins {
    id 'com.google.protobuf' version '0.9.4'
}

description 'Trident-Java gRPC core classes'

dependencies {
    implementation "com.google.guava:guava:$guavaVersion"

    implementation project(':utils')
    implementation project(':abi')

    implementation "io.grpc:grpc-netty-shaded:$grpcVersion"
    implementation "io.grpc:grpc-netty:$grpcVersion"
    implementation "io.grpc:grpc-okhttp:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    //json parsing
    implementation "com.google.protobuf:protobuf-java-util:$protobufVersion"

    implementation "org.bouncycastle:bcprov-jdk18on:$bouncycastleVersion"
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

sourcesJar {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion:osx-x86_64"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion:osx-x86_64"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                option 'jakarta_omit'
            }
        }
    }
}
